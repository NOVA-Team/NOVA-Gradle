def java_version = System.getProperty("java.runtime.version");
if (java_version.contains("+")) {
	java_version = java_version.substring(0, java_version.indexOf('+'));
}
if (java_version.contains("-")) {
	java_version = java_version.substring(0, java_version.indexOf('-'));
}
java_version = java_version.split("\\.");
def java_major_version = java.lang.Integer.parseInt(java_version[0]);
def java_minor_version = java_version.length >= 2 ? java.lang.Integer.parseInt(java_version[1]) : 0;
if (java_major_version == 1 && java_minor_version < 8) {
	throw new GradleException("Please use JDK 8 or above!")
}


configurations.all {
	resolutionStrategy.cacheChangingModulesFor 5, 'minutes'
}

repositories {
	mavenLocal()
	jcenter()
	maven {
		name "nova"
		url 'http://maven.novaapi.net/'
	}
}

// Basic ide plugin setup on every project
apply plugin: 'eclipse'
apply plugin: 'idea'

rootProject.extensions.findByName("idea")?.project.languageLevel = "1.8"

// Add writePom function
ext {
	writePom = { config ->
		{ xmlProvider ->
			def node = xmlProvider.asNode()

			node.appendNode("description", config["info.description"])
			node.appendNode("inceptionYear", config["info.inceptionYear"])
			node.appendNode("packaging", config["packaging"])
			node.appendNode("organisation")
			    .appendNode("name", config["info.organization.name"])
		}
	}
}
